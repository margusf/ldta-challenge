grammar ee.cyber.simplicitas.oberonexample.Oberon;

scalaheader {
import OberonExtras._
}

Module:
    "MODULE" name1=Id ";"
        decl=Declarations
        ("BEGIN"
            statements=StatementSequence)?
    "END" name2=Id ".";

StatementSequence: first=Statement (";" rest=Statement)*;

// TODO: this only exists so that we can add type to expression.
option Expression
    {var exprType: Any = null}
    : Id;

ConstantDef: name=Id EqOp expr=CompExpr ";";
VarDef: vars=IdentList ":" varType=Id ";";
ProcedureDecl:
    "PROCEDURE" name=Id
        ("(" (firstParam=FormalParam (";" rest=FormalParam)*)? ")")?
    ";"
    decl=Declarations
    ("BEGIN"
        body=StatementSequence)?
    "END" name2=Id;
FormalParam: pVar="VAR"? ids=IdentList ":" pType=Id;

Declarations:
    ("CONST"
        consts=ConstantDef*)?
    ("TYPE"
        types=TypeDef*)?
    ("VAR"
        vars=VarDef*)?
    (procedures=ProcedureDecl ";")*;

TypeDef: name=Id "=" tValue=Id ";";

CompExpr
    returns Expression {makeBinary(op, left, right)}
    : left=SimpleExpr (op=CompOp right=SimpleExpr)?;
option CompOp {def text: String}: EqOp | OtherCompOps;
terminal EqOp: "=";
terminal OtherCompOps: "#" | "<" | "<=" | ">" | ">=";

// TODO: fix issue 2869
option SimpleExpr returns Expression: PosExpr | NegExpr | PlusExpr;

PosExpr
    returns Expression {Unary(UnaryOp.Pos, arg)}
    : PlusOp arg=PlusExpr;
terminal PlusOp: "+";
terminal MinusOp: "-";
terminal OrOp: "OR";
NegExpr
    returns Expression {Unary(UnaryOp.Neg, arg)}
    : MinusOp arg=PlusExpr;
PlusExpr
    returns Expression {makeBinary(ops, first :: rest)}
    : first=Term (ops=PlusExprOp rest=Term)*;
option PlusExprOp
    {def text: String}
    : PlusOp | MinusOp | OrOp;

Term
    returns Expression {makeBinary(ops, first :: rest)}
    : first=Factor (ops=TermOp rest=Factor)*;
terminal TermOp: "*" | "DIV" | "MOD" | "&";
option Factor returns Expression:
    Lhs
    | NumberLit
    | ParenExpr
    | NotExpr;

option Lhs returns Expression:
    Id  // This is necessary to make Id inherit from Expression
    | SelectorExpr;

SelectorExpr
    returns Expression {makeSelector(id, selectors)}
    : id=Id selectors=SelectorPart+;
option SelectorPart: RecordSelector | ArraySelector;
RecordSelector: "." Id;
ArraySelector: "[" index=PlusExpr "]";

terminal NumberLit: ('0' .. '9')+;
ParenExpr returns Expression {expr}: "(" expr=CompExpr ")";
NotExpr
    returns Expression {Unary(UnaryOp.Not, expr)}
    : "~" expr=Factor;

IdentList: first=Id ("," rest=Id)*;

option Statement:
    Assignment
    | ProcedureCall
    | IfStatement
    | WhileStatement;

Assignment: left=Lhs ":=" right=CompExpr;
ProcedureCall:
    proc=Id /* TODO: Selector* */  // WTF is selector doing here?
        ("(" first=CompExpr ("," rest=CompExpr)* ")")?;

IfStatement:
    "IF" cond=CompExpr "THEN"
        ifStmt=StatementSequence
    ("ELSIF" elsifCond=CompExpr "THEN"
        elsifStmt=StatementSequence)*
    ("ELSE"
        elseStmt=StatementSequence)?
    "END";

WhileStatement:
    "WHILE" cond=CompExpr "DO"
        body=StatementSequence
    "END";

terminal Id
    {var ref: CommonNode = null}
    : ('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'0'..'9')*;
fragment MlComment: '/*' (~'*' | '*' ~'/')* '*/';
fragment SlComment: '//' ~('\n'|'\r')*;
hidden terminal WS: (' '|'\t'|'\r'|'\n'|SlComment|MlComment)+;
